package sbnz.integracija.rules;

import better.me.enums.*;
import better.me.model.*;
import better.me.modelDB.*;
import better.me.dto.*;
import better.me.util.*;
import java.util.ArrayList;
import java.util.List;

global MyLogger myLogger;

rule "Filter meals by diet"
	salience Integer.MAX_VALUE
	agenda-group "filter"
	no-loop
	lock-on-active
	when
		$allMeals: List(size > 0)
		$user: RegisteredUser($diet: diet)
        $meals: List ()  from accumulate ( 
			Meal ($meal: this, $ingredients: ingredients) from $allMeals,
			init( List<Meal> notDietMeals = new ArrayList(); ),
			action(  
				for (Object ingredient : $ingredients) {
					if (((Ingredient)ingredient).getGrocery().getDiet().compareTo(Diet.valueOf($diet)) > 0) {
						myLogger.log("adding meal not diet: " + $meal.getName() + " because of ingredient: " + ((Ingredient)ingredient).getGrocery().getName());
						notDietMeals.add($meal);
						break;
					}
				}
			 ),
			result ( notDietMeals )
		)
	then
		myLogger.log("Filtering meals by diet. Removed meals: " + $meals.size());
		modify ( $allMeals ) { removeAll($meals); }
end	

rule "Filter meals by allergens"
	salience 100
	agenda-group "filter"
	lock-on-active
	when
        $user: RegisteredUser($allergens:allergens, $allergens != null)
        $allMeals: List(size > 0)
        $notOkMeals: List(size == 0)
		$meals : List() from accumulate ( 
			$meal: Meal($ingredients: ingredients) from $allMeals,
			init( List<Meal> withAllergens = new ArrayList(); ),
			action(
				myLogger.log("allMeals.size() = " + $allMeals.size());
				for (Object ingredient : $ingredients) {
					for (Object allergen : $allergens) {
						if (((Allergen)allergen).getName().equals( ((Ingredient)ingredient).getGrocery().getName())) {
							withAllergens.add($meal);
							break;
						}
					}
				}
			 ),
			result ( withAllergens )
		)
	then 
		myLogger.log("2. allMeals.size() = " + $allMeals.size());
		myLogger.log("Filtering meals by allergens");
		modify ($notOkMeals) {addAll($meals);}
end

rule "Filter meals by preparation time"
	agenda-group "filter"
	lock-on-active
	no-loop true
	when
		$allMeals: List()
		$filter: FilterDTO($fromTime:fromTime, $fromTime != 0, $toTime: toTime, $toTime != 0)
		$meals : List () from collect ( 
			Meal(time < $fromTime || time > $toTime) from $allMeals
		)
	then 
		myLogger.log("Filtering meals by preparation time");
		modify ( $allMeals ) { removeAll($meals); }
end

rule "Filter meals by name"
	agenda-group "filter"
	no-loop true
	lock-on-active
	when
		$allMeals: List()
		$filter: FilterDTO($name: name, $name != "")
        $meals : List () from collect ( 
        	Meal(name contains $name) from $allMeals
        )
	then
		myLogger.log("Filtering meals by name");
		modify ( $allMeals ) { removeAll($allMeals); }
		modify ( $allMeals) { addAll($meals); }
end	
		
rule "Sort meals by calories ascending"
	salience -$cals
	agenda-group "filter"
	when
		$filter: FilterDTO(descending == false)
	    $sorted: SortedMeals( $sortedList: sortedList)
	    $meal: Meal( $cals: calories)
	    $allMeals: List(size > 0, this contains $meal)
	then
		myLogger.log("Sorting meals by calories ascending");
		modify( $sorted ){ getSortedList().add( $meal ) }
		delete($meal)
end

rule "Sort meals by calories descending"
	salience -Integer.MAX_VALUE + $cals 
	agenda-group "filter"
	when
		$filter: FilterDTO(descending == true)
		$sorted: SortedMeals( $sortedList: sortedList)
		$meal: Meal( $cals: calories)
	    $allMeals: List(size > 0, this contains $meal)
	then
		myLogger.log("Sorting meals by calories descending" + $meal.getName());
		modify( $sorted ){ getSortedList().add( $meal ) }
		delete($meal)
end
