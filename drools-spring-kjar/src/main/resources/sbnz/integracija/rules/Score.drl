package sbnz.integracija.rules;

import better.me.enums.*;
import better.me.model.*;
import better.me.dto.*;
import better.me.modelDB.*;
import better.me.util.*;

global MyLogger myLogger;

rule "Calculating score for previous week"
	agenda-group "score"
	no-loop
    when
    	$user: RegisteredUser($id: id, $score: score)
    	$week: Week($days: days, $goalCals: goalCalories, $goalProteins: goalProteins, $goalFats: goalFats, $goalCarbs: goalCarbs)
    	checkIfWholeWeekIsSubmitted($week;)
    	$daysCaloriesOK: Number(intValue >= 0) from accumulate (
       		$day: Day() from $days,
			init( int ok = 0; ),
			action(
				if ($day.getCalories() >= $goalCals * 0.9 && $day.getCalories() <= $goalCals * 1.1) {
					ok += 1;
				}
			 ),
			result ( ok )
        )
        $daysMacroOK: Number(intValue >= 0) from accumulate (
       		$day: Day() from $days,
			init( int ok = 0; ),
			action(
				if ($day.getProteins() >= $goalProteins * 0.9 && $day.getProteins() <= $goalProteins * 1.1) {
					ok += 1;
				}
				if ($day.getCarbs() >= $goalCarbs * 0.9 && $day.getCarbs() <= $goalCarbs * 1.1) {
					ok += 1;
				}
				if ($day.getFats() >= $goalFats * 0.9 && $day.getFats() <= $goalFats * 1.1) {
					ok += 1;
				}
			 ),
			result ( ok )
        )
    then
        myLogger.log("Calculating score for previous week");
        modify ($user) {setScore($score + $daysCaloriesOK.intValue() * 10 + $daysMacroOK.intValue() * 3);}
        myLogger.log("Changed agenda-group from score to category");
		kcontext.getKnowledgeRuntime().getAgenda().getAgendaGroup("category").setFocus();
end
